application itemisApp {
	background="Background.png"
	tab {
		title: "News"
		icon: "08-chat.png"
		view: BlogList( Blogposts() )
	}
   
	tab {
		title: "Talks"   
		icon: "66-microphone.png"
		view: VortragListe( AllVortragItems() )
	}

	tab {
		title: "Speakers"
		icon: "person.png"
		view: SprecherListe( AllSprecherItems() )
	}
	
	tab {
		title: "Barcode"
		icon: "123-id-card.png"
		view: BarcodeScanner()
	}
}

/*
 * DESCRIBING DATA
 */

type String mapsTo "NSString"

entity BlogItem {
	String title
	String author
	String link
	String description
	String pubDate
	BlogItem subItem
}

entity Sprecher {
	String name
	String beschreibung
	String email
	String blog
	String fotourl
	String vortraege
}

entity Vortrag {
	String titel
	String untertitel
	String sprecher
	String beschreibung
	String zeit
	Vortrag fortsetzung
}



/*
 * FETCHING DATA
 */


contentprovider Blogposts
	returns BlogItem[]
	fetches XML
		from "http://blogs.itemis.de/?showfeed=1"
		selects "rss.channel.item"
	
contentprovider AllVortragItems
	returns Vortrag[]
	fetches XML
		from "http://spreadsheets.google.com/feeds/list/0Au3-oaNYhfPIdEpRQWxpZnJyX2JCNUdtT1Z4M1B4SkE/1/public/values"
		selects "feed.entry"
		
contentprovider VortragByTitel(String titel)
	returns Vortrag
	fetches XML
		from ("http://spreadsheets.google.com/feeds/list/0Au3-oaNYhfPIdEpRQWxpZnJyX2JCNUdtT1Z4M1B4SkE/1/public/values?sq=vortragid%3D" urlconform(replace(titel, " ", "")))
		selects "feed.entry"
		
contentprovider AllSprecherItems
	returns Sprecher
	fetches XML
		from "http://spreadsheets.google.com/feeds/list/0Au3-oaNYhfPIdEpRQWxpZnJyX2JCNUdtT1Z4M1B4SkE/2/public/values"
		selects "feed.entry"

contentprovider SprecherByName(String name)
	returns Sprecher
	fetches XML
		from ("http://spreadsheets.google.com/feeds/list/0Au3-oaNYhfPIdEpRQWxpZnJyX2JCNUdtT1Z4M1B4SkE/2/public/values?sq=sprecherid%3D" urlconform(replace(name, " ", "")))
		selects "feed.entry"
		
		
		
		
		
/*
 * DESCRIBING VIEWS
 */		
 
customview BarcodeScanner class "BarcodeScannerViewController" 

tableview BlogList(BlogItem[] items) {
	title: "News"
	section {
		cell Subtitle foreach items as i {
			text: i.author
			details: i.title
			image: ("http://blogs.itemis.de/wp-content/themes/itemis-WP-Theme/photos/" urlconform(i.author) ".jpg")
			action: BlogDetails(i)   
		}
	} 
}  

detailsview BlogDetails(BlogItem item) {
	title: "News" 
	header {
		title: item.title
		details: item.description
		image: ("http://blogs.itemis.de/wp-content/themes/itemis-WP-Theme/photos/"urlconform(item.author)".jpg")
	}
	section {
		cell Default {
			text: item.author
			action: SprecherDetails(SprecherByName(item.author))
		}
		cell Default {
			text: "Open in Safari"
			action: item.link
		}
	}
} 

tableview VortragListe(Vortrag[] items) {
	title: "Talks"
	section {
		cell Subtitle foreach items as i {
			text: i.titel
			details: i.zeit
			action: VortragDetailsView( i )   
		} 
	}
}

detailsview VortragDetailsView(Vortrag vortrag) {
	title: "Talk"
	header {
		title: vortrag.titel
		subtitle: (vortrag.untertitel "\\n" vortrag.zeit)
		details: vortrag.beschreibung
	}
	
	section {
		cell DefaultWithDisclosure foreach split(vortrag.sprecher, ", ") as s {
			text: s
			action: SprecherDetails(SprecherByName(s))		
		}
		
	}
	
}

tableview SprecherListe(Sprecher[] sprecher) {
	title: "Speakers"
	section {
		cell Default foreach sprecher as s {
			text: s.name
			image: s.fotourl
			action: SprecherDetails(s)   
		}
	}
}

detailsview SprecherDetails(Sprecher sprecher) {
	title: "Speaker"
	header {
		title: sprecher.name
		details: sprecher.beschreibung
		image: sprecher.fotourl
	}
	
	
	section {
		cell Default foreach split(sprecher.vortraege, ", ") as titel {
			text: titel
			action: VortragDetailsView(VortragByTitel(titel))
		}
	}
	
	section {
		cell Value2 {
			text: "mail"
			details: sprecher.email
			action: ("mailto:"sprecher.email) 
		}
		cell Value2 {
			text: "blog"
			details: sprecher.blog
			action: ("http://"sprecher.blog) 
		}
	}
}

